// AMOGUS SUS description language

_sep{x, s}: [x (s x)*]

start : thing*
?thing : inclusion | setting | compound
         | enum | bitfield | confirmation
         | global_method | entity

inclusion : "include" PATH
PATH      : /[\w.:\/\\][\w.:\/\\ ]*/

setting   : "set" PARAMETER VALUE
PARAMETER : /[a-z](_|[a-z])+/
VALUE     : /.+/

enum          : [DOCSTRING] "enum" "(" NUMBER ")" TYPE_IDENTIFIER "{" _sep{enum_member, ","} "}"
bitfield      : [DOCSTRING] "bitfield" "(" NUMBER ")" TYPE_IDENTIFIER "{" _sep{enum_member, ","} "}"
confirmation  : [DOCSTRING] "confirmation" TYPE_IDENTIFIER "(" NUMBER ")" "{" conf_request conf_response "}"
conf_request  : "request" "{" method_param* "}"
conf_response : "response" "{" method_param* "}"
egm_field     : FIELD_IDENTIFIER ":" type

ROOT_IDENTIFIER      : /[A-Z][A-Za-z]*/
FIELD_IDENTIFIER     : /[a-z][a-z_]*/
METHOD_IDENTIFIER    : /[a-z][a-z_]*/
enum_member          : [DOCSTRING] FIELD_IDENTIFIER "(" NUMBER ")"
VALIDATOR_IDENTIFIER : /[a-z][a-z_]+/
TYPE_IDENTIFIER      : /[A-Z][A-Za-z]+/

value         : SIGNED_NUMBER | BOOL | STRING | list
NUMBER        : /[0-9]+/
SIGNED_NUMBER : /-?[0-9]+/
REGEX         : /\/.+\/i?m?s?/
BOOL          : "false" | "true"
STRING        : /".*"/
list          : "[" _sep{value, ","} "]"

DOCSTRING : /@>(((?<!<)@)|((?<! )<)|[^<@])+<@/ // yep!

range   : SIGNED_NUMBER ".." SIGNED_NUMBER | SIGNED_NUMBER "+"
TIMEOUT : NUMBER ("ms" | "s" | "m" | "h" | "d" | "mo" | "y")


type            : TYPE_IDENTIFIER ["(" _sep{type_argument, ","} ")"] ["[" _sep{type_validator, ","}*"]"]
type_argument   : type | NUMBER
type_validator  : VALIDATOR_IDENTIFIER ":" validator_value
validator_value : SIGNED_NUMBER | range | REGEX


entity            : [DOCSTRING] "entity" ROOT_IDENTIFIER "(" NUMBER ")" "{" entity_directive* "}"
?entity_directive : static_method | normal_method | entity_field
entity_field      : [DOCSTRING] FIELD_IDENTIFIER ":" [field_opt] type ";"
?field_opt        : "opt" "(" NUMBER ")"

_method{kw}       : [DOCSTRING] kw METHOD_IDENTIFIER "(" NUMBER ")" "{" method_directive* "}"
static_method     : _method{"staticmethod"}
normal_method     : _method{"method"}
global_method     : _method{"globalmethod"}
?method_directive : method_param | returns | errors | confirmations | rate_limit
method_param      : [DOCSTRING] FIELD_IDENTIFIER ":" [field_opt] type ["=" value] ";"
returns           : "returns" "{" method_param* "}"
errors            : "errors" "{" _sep{FIELD_IDENTIFIER, ","} "}"
confirmations     : "confirmations" "{" _sep{ROOT_IDENTIFIER, ","} "}"
rate_limit        : "ratelimit" NUMBER "every" TIMEOUT ";"

compound       : [DOCSTRING] "compound" ROOT_IDENTIFIER "{" compound_field* "}"
compound_field : [DOCSTRING] FIELD_IDENTIFIER ":" [field_opt] type ";"

COMMENT: /#.*$/m
%ignore COMMENT

%import common.WS
%ignore WS